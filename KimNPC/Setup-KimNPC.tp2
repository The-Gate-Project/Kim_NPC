BACKUP ~KimNPC/backup~
AUTHOR ~grenouillebleue@hotmail.com~

VERSION ~v1.7~

ALWAYS

    // Copies tra files into the autotra-folder (to leave the originals untouched)
    DEFINE_ACTION_FUNCTION autotra_workaround BEGIN
      COPY ~%MOD_FOLDER%/Traductions/English~  ~%MOD_FOLDER%/Traductions/autotra/%LANGUAGE%~
      COPY ~%MOD_FOLDER%/Traductions/%LANGUAGE%~  ~%MOD_FOLDER%/Traductions/autotra/%LANGUAGE%~
    END

    LAF autotra_workaround END

// ACTION_DEFINE_ARRAY fl#noconvert BEGIN END

ACTION_DEFINE_ARRAY fl#reload BEGIN setup END

    LAF HANDLE_CHARSETS
      INT_VAR
        infer_charsets = 1
      STR_VAR
        tra_path = EVAL ~%MOD_FOLDER%/Traductions/autotra~
        // noconvert_array = fl#noconvert
        reload_array = fl#reload
	END
	
	INCLUDE ~KimNPC/Lib/gw_functions.tpa~
	
	ACTION_IF NOT VARIABLE_IS_SET bg2_chapter BEGIN
	ACTION_IF GAME_IS ~eet~ BEGIN
		OUTER_SET bg2_chapter = 12
	END ELSE BEGIN
		OUTER_SET bg2_chapter = 0
	END
	OUTER_FOR (i=1; i<=10; i=i+1) BEGIN
		OUTER_SET bg2_chapter = bg2_chapter + 1
		OUTER_SPRINT name_source ~bg2_chapter_%i%~
		OUTER_SET EVAL ~%name_source%~ = bg2_chapter
		END
	END
END

AUTO_TRA ~KimNPC/Traductions/autotra/%s~

LANGUAGE ~Francais~
	 ~Francais~
	 ~KimNPC/Traductions/Francais/Setup.tra~

LANGUAGE ~Deutsch (by Xicloing)~
	 ~Deutsch~
	 ~KimNPC/Traductions/Deutsch/Setup.tra~

     LANGUAGE ~English (by Xicloing)~
	 ~English~
	 ~KimNPC/Traductions/English/Setup.tra~

BEGIN @0
LABEL ~Grenouillebleue-Kim_Main~

COPY ~KimNPC/sons~ ~override~

PRINT @51

COPY ~KimNPC/Cre/Kim.cre~ ~override/Kim.cre~
  SAY NAME1 ~Kim~
  SAY NAME2 ~Kim~
  SAY LEADER @120 /* Très bon choix. [Kima] */                                                  
  SAY TIRED @121  /* Je veux dormir. [Kimb] */                                                 
  SAY BORED @122  /* J'm'embête. [Kimc] */                                                     
  SAY BATTLE_CRY1 @123  /* À l'abordage ! [Kimd] */                                                  
  SAY BATTLE_CRY2 @124  /* En garde ! [Kime] */                                                      
  SAY DAMAGE @125  /* Mais ! Aïe ! [Kimf] */                                                    
  SAY DYING @126  /*  [Kimg] */                                                                
  SAY HURT @127  /* Bon vous vous décidez à me soigner ? [Kimh] */                            
  SAY AREA_FOREST @128  /* Toute cette verdure ne vaut pas la mer. [Kimi] */                         
  SAY AREA_CITY @129  /* Ah les villes, pleins de gens à détrousser. [Kimj] */                     
  SAY AREA_DUNGEON @130 /* Ça sent pas bon. [Kimk] */                                                 
  SAY SELECT_COMMON1 @131 /* Quoi encore ? [Kiml] */                                                    
  SAY SELECT_COMMON2 @132 /* Oui ! [Kimm] */                                                            
  SAY SELECT_COMMON3 @133 /* Me regarde pas comme ça ! [Kimn] */                                        
  SAY SELECT_COMMON4 @134 /* J'ai soif ! File-moi du rhum ! [Kimo] */                                   
  SAY SELECT_COMMON5 @135 /* Ouais ? [Kimp] */                                                          
  SAY SELECT_COMMON6 @136 /* Besoin de ma lame ? [Kimq] */                                              
  SAY SELECT_ACTION1 @137 /* C'est bien pour te faire plaisir. [Kimr] */                                
  SAY SELECT_ACTION2 @138 /* Tu peux pas demander à quelqu'un d'autre ? [Kims] */                       
  SAY SELECT_ACTION3 @139 /* Où tu veux, quand tu veux.  [Kimt] */                                      
  SAY SELECT_ACTION4 @140 /* T'es sûr que tu sais ce que tu fais ? [Kimu] */                            
  SAY SELECT_ACTION5 @141 /* Bon, dépêchons-nous ! [Kimv] */                                            
  SAY CRITICAL_HIT @142 /* Et voilà encore la magnifique Kim à l’œuvre. [Kimw] */                     
  SAY CRITICAL_MISS @143 /* Oups, c'est pas ma faute. [Kimx] */                                        
  SAY TARGET_IMMUNE @144 /* Y fait rien qu'à faire qu'à pas être blessé... [Kimy] */                   
  SAY INVENTORY_FULL @145 /* Bon aller reprend tout ton bazar, je suis pas une poubelle ! [Kimz] */
  SAY PICKED_POCKET @146 /* Hé hop, les sous-sous dans la po-poche. [Kimaa] */                         
  SAY HIDDEN_IN_SHADOWS @147 /* Je suis cachée. [Kimab] */                                                 
  SAY SPELL_DISRUPTED @148 /* Mon sort est interrompu. [Kimac] */                                        
  SAY SET_A_TRAP @149 /* Ça va faire mal ! [Kimad] */  
  
 ACTION_IF GAME_IS ~bg2ee eet~ BEGIN
 
	COPY_EXISTING ~Kim.cre~ ~override~
	REMOVE_CRE_ITEM ~TTPOT~ ~qitem1~
	ADD_CRE_ITEM ~Potn52~ #2 #0 #0 ~IDENTIFIED~ ~qitem1~
		
END 

// Avoid to use same death variable that original game creature

COPY_EXISTING ~Lich01.cre~ ~override/kimlich.cre~
WRITE_ASCII 0x280 ~kimlich~ #8

COPY_EXISTING ~Tanari01.cre~ ~override/kimtana.cre~
WRITE_ASCII 0x280 ~kimtana~ #8

COPY ~KimNPC/Cre/VampKim.cre~ ~override/VampKim.cre~
  SAY NAME1 @1
  SAY NAME2 @1


COPY ~KimNPC/Cre/UmbKim.cre~ ~override/UmbKim.cre~
  SAY NAME1 @2
  SAY NAME2 @2

COPY ~KimNPC/Cre/SkullKim.cre~ ~override/SkullKim.cre~
  SAY NAME1 @3
  SAY NAME2 @3

COPY ~KimNPC/Cre/Barbe3.cre~ ~override/Barbe3.cre~
  SAY NAME1 @3
  SAY NAME2 @3

COPY ~KimNPC/Cre/KimSolar.cre~ ~override/KimSolar.cre~
  SAY NAME1 @4
  SAY NAME2 @4

COPY ~KimNPC/Cre/ImpKim.cre~ ~override/ImpKim.cre~
  SAY NAME1 @5
  SAY NAME2 @5

COPY ~KimNPC/Cre/ImpKim2.cre~ ~override/ImpKim2.cre~
  SAY NAME1 @6
  SAY NAME2 @6


COPY ~KimNPC/Cre/Barbe1.cre~ ~override/Barbe1.cre~
  SAY NAME1 @3
  SAY NAME2 @3

COPY ~KimNPC/Cre/Barbe2.cre~ ~override/Barbe2.cre~
  SAY NAME1 @3
  SAY NAME2 @3

COPY ~KimNPC/Cre/Zven.cre~ ~override/Zven.cre~
  SAY NAME1 @7
  SAY NAME2 @7


COPY ~KimNPC/Cre/ImpMob.cre~ ~override/ImpMob.cre~
  SAY NAME1 @8
  SAY NAME2 @8


PRINT @52

COPY ~KimNPC/Items/AmulKim.itm~ ~override/AmulKim.itm~
  SAY NAME1 @10
  SAY NAME2 @11
  SAY UNIDENTIFIED_DESC @12
  SAY DESC @13

COPY ~KimNPC/Items/BarbeAxe.itm~ ~override/BarbeAxe.itm~
  SAY NAME1 @15
  SAY NAME2 @16
  LPF ~GW_WRITE_EE_ITM_DESCRIPTIONS~ STR_VAR GW_undesc_to_update = "@17" END
  LPF ~GW_WRITE_EE_ITM_DESCRIPTIONS~ STR_VAR GW_desc_to_update = "@18" END


COPY ~KimNPC/Items/CartBar2.itm~ ~override/CartBar2.itm~
  SAY NAME1 @20
  SAY NAME2 @21
  SAY UNIDENTIFIED_DESC @22
  SAY DESC @23
COPY ~KimNPC/Items/CartBar.itm~ ~override/CartBar.itm~
  SAY NAME1 @20
  SAY NAME2 @25
  SAY UNIDENTIFIED_DESC @21
  SAY DESC @26
COPY ~KimNPC/Items/Helm30b.itm~ ~override/Helm30b.itm~
  SAY NAME2 @30
  SAY DESC @31
COPY ~KimNPC/Items/KimChain.itm~ ~override/KimChain.itm~
  SAY NAME1 @35
  SAY NAME2 @36
  LPF ~GW_WRITE_EE_ITM_DESCRIPTIONS~ STR_VAR GW_undesc_to_update = "@37" END
  LPF ~GW_WRITE_EE_ITM_DESCRIPTIONS~ STR_VAR GW_desc_to_update = "@38" END

COPY ~KimNPC/Items/UrneKim.itm~ ~override/UrneKim.itm~
  SAY NAME2 @40
  SAY DESC @41

COPY ~KimNPC/Items/KimBody.itm~ ~override/KimBody.itm~
  SAY NAME2 @45
  SAY DESC @46



COPY ~KimNPC/images~ ~override~

// Area

COPY~KimNPC/Are/ArBarb.are~ ~override/ArBarb.are~

// Pirates

COPY ~KimNPC/Cre/Pirate1.cre~ ~override/Pirate1.cre~
  WRITE_ASCII 0x248 ~KimPira~ (8) // Override script
COPY ~KimNPC/Cre/Pirate2.cre~ ~override/Pirate2.cre~
  WRITE_ASCII 0x248 ~KimPira~ (8) // Override script
COPY ~KimNPC/Cre/Pirate3.cre~ ~override/Pirate3.cre~
  WRITE_ASCII 0x248 ~KimPira~ (8) // Override script
COPY ~KimNPC/Cre/Pirate4.cre~ ~override/Pirate4.cre~
  WRITE_ASCII 0x248 ~KimPira~ (8) // Override script

PRINT @56
// Adds CD_STATE_NOTVALID state
APPEND ~STATE.IDS~ ~0x80101FEF CD_STATE_NOTVALID~
    UNLESS ~CD_STATE_NOTVALID~
    
APPEND ~STATE.IDS~ ~0x00000FC0 STATE_REALLY_DEAD~ 
    UNLESS ~0x00000FC0 STATE_REALLY_DEAD~

APPEND ~GTIMES.IDS~   ~6 ONE_ROUND~     
    UNLESS ~ONE_ROUND~
APPEND ~spell.ids~ ~2302 WIZARD_REMOVE_MAGIC~ 
    UNLESS ~WIZARD_REMOVE_MAGIC~ // dupe of WIZARD_DISPEL_MAGIC



COMPILE ~KimNPC/Baf/CutKim2.baf~

COMPILE ~KimNPC/Baf/Kim.baf~
		EVALUATE_BUFFER
		
COMPILE ~KimNPC/Baf/VampKim.baf~
        ~KimNPC/Baf/KimCut53.baf~

COMPILE ~KimNPC/Baf/ImpKim.baf~
        ~KimNPC/Baf/ImpKim2.baf~
        ~KimNPC/Baf/KimSolar.baf~
        ~KimNPC/Baf/Barbe3.baf~
        ~KimNPC/Baf/SkullKim.baf~
        ~KimNPC/Baf/CutKim3.baf~
        ~KimNPC/Baf/CutKim4.baf~

COMPILE ~KimNPC/Baf/Barbe.baf~
        ~KimNPC/Baf/Barbe2.baf~
        ~KimNPC/Baf/Zven.baf~
        ~KimNPC/Baf/KimPira.baf~
        
COMPILE  ~KimNPC/Baf/CutPira1.baf~


COMPILE ~KimNPC/Baf/UmbKim.baf~


PRINT @53
ACTION_IF GAME_IS ~bg2ee eet~ BEGIN
	COPY ~KimNPC/images/EE~ ~override~
END

COMPILE EVALUATE_BUFFER ~KimNPC/Dlg/KimJ.d~
  USING ~KimNPC/Traductions/autotra/%s/KimJ.tra~

COMPILE ~KimNPC/Dlg/Kim.d~
  USING ~KimNPC/Traductions/autotra/%s/Kim.tra~

COMPILE ~KimNPC/Dlg/Banters.d~
  USING ~KimNPC/Traductions/autotra/%s/Banters.tra~

COMPILE ~KimNPC/Dlg/KimInterject.d~
  USING ~KimNPC/Traductions/autotra/%s/KimInterject.tra~

COMPILE ~KimNPC/Dlg/VampKim.d~
  USING ~KimNPC/Traductions/autotra/%s/VampKim.tra~

COMPILE ~KimNPC/Dlg/SkullKim.d~
  USING ~KimNPC/Traductions/autotra/%s/SkullKim.tra~

COMPILE ~KimNPC/Dlg/Quete.d~
  USING ~KimNPC/Traductions/autotra/%s/Quete.tra~

COMPILE ~KimNPC/Dlg/ImpKim2.d~
  USING ~KimNPC/Traductions/autotra/%s/ImpKim2.tra~

COMPILE ~KimNPC/Dlg/ImpKim.d~
  USING ~KimNPC/Traductions/autotra/%s/ImpKim.tra~

COMPILE ~KimNPC/Dlg/Barbe3.d~
  USING ~KimNPC/Traductions/autotra/%s/Barbe3.tra~

COMPILE ~KimNPC/Dlg/KimSolar.d~
  USING ~KimNPC/Traductions/autotra/%s/KimSolar.tra~

COMPILE ~KimNPC/Dlg/Zven.d~
  USING ~KimNPC/Traductions/autotra/%s/Zven.tra~

COMPILE ~KimNPC/Dlg/Barbe.d~
  USING ~KimNPC/Traductions/autotra/%s/Barbe.tra~

COMPILE ~KimNPC/Dlg/Barbe2.d~
  USING ~KimNPC/Traductions/autotra/%s/Barbe2.tra~

COMPILE ~KimNPC/Dlg/UmbKim.d~
  USING ~KimNPC/Traductions/autotra/%s/UmbKim.tra~


PRINT @54

// AR1604.ARE have wrong and inexisting script assigned

COPY_EXISTING ~AR1604.ARE~ ~override~
  WRITE_ASCII 0x94 ~AR1604~ #8
  BUT_ONLY

ACTION_IF (FILE_EXISTS_IN_GAME ~AR1604.bcs~) THEN BEGIN

  EXTEND_TOP ~AR1604.bcs~ ~KimNPC/Baf/Kimumberlee.baf~

END ELSE BEGIN

COMPILE ~KimNPC/Baf/Kimumberlee.baf~	~override/AR1604.bcs~

END

// EXTEND_BOTTOM ~Ar0300.bcs~ ~KimNPC/Baf/KimAr0300.baf~

// Ar0306.ARE don't have script assigned

COPY_EXISTING ~Ar0306.ARE~ ~override~
  WRITE_ASCII 0x94 ~AR0306~ #8
  BUT_ONLY

ACTION_IF (FILE_EXISTS_IN_GAME ~Ar0306.bcs~) THEN BEGIN

  EXTEND_TOP ~Ar0306.bcs~ ~KimNPC/Baf/KimAr0300.baf~

END ELSE BEGIN

COPY ~KimNPC/Baf/KimAr0300.baf~	~override/AR0306.bcs~
 COMPILE_BAF_TO_BCS
 
END

EXTEND_BOTTOM ~Ar0809.bcs~ ~KimNPC/Baf/KimAr0809.baf~
EXTEND_BOTTOM ~Vampamb.bcs~ ~KimNPC/Baf/KimVampAmb.baf~
EXTEND_BOTTOM ~cleanse.bcs~ ~KimNPC/Baf/KimCleanse.baf~
EXTEND_BOTTOM ~bodhiamb.bcs~ ~KimNPC/Baf/KimBodhiAmb.baf~

APPEND ~pdialog.2da~
~Kim      KimP           KimJ          KimD~
UNLESS ~Kim~
UNLESS ~25POST~

APPEND ~pdialog.2da~
~Kim      KimP           KimJ          KimD       Kim25P           Kim25J              Kim25D              Kimd25~
UNLESS ~Kim~
IF ~25POST~


PRINT @55

AT_INTERACTIVE_EXIT ~KimNPC/Kimaudio.bat~
AT_INTERACTIVE_UNINSTALL ~KimNPC/Kimaudiouninstall.bat~





/////////////////////////////////////////////////////////////////////////////////
// Hache réajustée

BEGIN @100
LABEL ~Grenouillebleue-Kim_Axe~

REQUIRE_FILE ~override/BarbeAxe.itm~ @102

COPY ~KimNPC/Items/BarbeAx2.itm~ ~override/BarbeAxe.itm~
  SAY NAME1 @15
  SAY NAME2 @16
  LPF ~GW_WRITE_EE_ITM_DESCRIPTIONS~ STR_VAR GW_undesc_to_update = "@17" END
  LPF ~GW_WRITE_EE_ITM_DESCRIPTIONS~ STR_VAR GW_desc_to_update = "@101" END
